# MIT License
#
# Copyright (c) 2022 Graur Andrew
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias number org.eolang.math.number
+alias org.eolang.hamcrest.matchers.all-of-matcher
+alias org.eolang.hamcrest.matchers.any-of-matcher
+alias org.eolang.hamcrest.matchers.anything-matcher
+alias org.eolang.hamcrest.matchers.equal-to-matcher
+alias org.eolang.hamcrest.matchers.greater-than-matcher
+alias org.eolang.hamcrest.matchers.not-matcher
+alias org.eolang.collections.list
+alias org.eolang.txt.text
+alias sprintf org.eolang.txt.sprintf
+home https://github.com/objectionary/eo-hamcrest
+package org.eolang.hamcrest
+rt jvm org.eolang:eo-hamcrest:0.0.0
+version 0.0.0

# Main object for assertions
[actual matcher reasons...] > assert-that
  actual > act!

  if. > @
    matcher.match act
    TRUE
    sprintf
      "%s\nExpected: %s\n     but: %s"
      if.
        is-empty.
          list
            reasons
        ""
        reasons.at 0
      description-of.
        matcher
      describe-mismatch.
        matcher
        act

  # Is the value equal to another value
  [x] > equal-to
    equal-to-matcher x > @

  # Calculates the logical negation of a matcher
  [matcher] > not
    not-matcher matcher > @

  # Calculates the logical conjunction of multiple matchers.
  # Evaluation is shortcut, so subsequent matchers are not called
  # if an earlier matcher returns false.
  [matchers...] > all-of
    all-of-matcher (...matchers) > @

  # Calculates the logical disjunction of multiple matchers.
  # Evaluation is shortcut, so subsequent matchers are not called
  # if an earlier matcher returns true.
  [matchers...] > any-of
    any-of-matcher (...matchers) > @

  # Compare object that matches when the examined object
  # is greater than the specified value
  [x] > greater-than
    greater-than-matcher x > @

  # Compare object that matches when the examined object
  # is less than the specified value
  [x] > less-than

    [a] > match
      lt. > @
        a
        x

    [act] > describe-mismatch
      sprintf > @
        "<%s> was greater than <%s>"
        act
        x

    [] > description-of
      sprintf > @
        "<%s> greater than value"
        x

  # Compare object that matches when an examined float
  # is equal to the specified operand, within a range of +/- error.
  [operand err] > close-to

    memory 0 > mismatch

    [a] > match
      seq > @
        mismatch.write actual-delta
        actual-delta.lte 0.0
      [] > actual-delta
        minus. > @
          abs.
            number (a.minus operand)
          err

    [act] > describe-mismatch
      sprintf > @
        "<%s> differed by <%.4s> more than delta <%s>"
        act
        mismatch
        err

    [] > description-of
      sprintf > @
        "a numeric value within <%s> of <%s>"
        operand
        err

  # Decorates another Matcher, retaining the behaviour
  # but allowing tests to be slightly more expressive.
  [matcher] > is

    matcher > @

    [] > description-of
      sprintf > @
        "is %s"
        description-of.
          matcher

  # A matcher that always returns true.
  [] > anything
    anything-matcher > @

  # Wraps an existing matcher, overriding its description with that specified.
  # All other functions are delegated to the decorated matcher,
  # including its mismatch description.
  [matcher format args...] > described-as

    matcher > @

    # @todo #3:45min Here we got an object printed
    #  instead of data. The .map [i] i.as-string didn't work.
    #  We need to convert varargs to data[] somehow.
    [] > description-of
      sprintf > @
        format
        args

  # Wraps an existing matcher and apply it to every item in array.
  [matcher] > has-item
    memory 0 > mismatches

    [a] > match
      [s] > apply
        if. > @
          matcher.match s
          TRUE
          seq
            mismatches.write
              mismatches.with
                matcher.describe-mismatch act
            FALSE
      seq > @
        mismatches.write *
        reducedi.
          list
            act
          FALSE
          [acc i x]
            or. > @
              acc
              apply x

    # @todo #37:30min This string formatting doesn't work correctly.
    #  Here we got message like: [was <[Lorg.eolang.Phi;@1189dd52>]
    #  and possibly we need to change code below when string manipulation
    #  objects will be created.
    [act] > describe-mismatch
      sprintf > @
        "mismatches: [%s]"
        (text "").joined mismatches

    [] > description-of
      sprintf > @
        "an array containing %s"
        description-of.
          matcher

  # Wraps an array of matchers and apply them to every item in array.
  [matchers...] > has-items
    memory 0 > mismatches

    [a] > all-match
      memory 0 > count
      seq > @
        count.write 0
        while.
          and.
            count.lt (matchers.length)
            count.gte 0
          [i]
            if. > @
              (matchers.at i).match a
              count.write (i.plus 1)
              seq
                mismatches.write
                  mismatches.with
                    describe-mismatch.
                      matchers.at i
                      act
                count.write -1
        count.gt 0

    [a] > match
      seq > @
        mismatches.write *
        reducedi.
          list
            act
          FALSE
          [acc i el]
            or. > @
              acc
              all-match el

    [act] > describe-mismatch
      sprintf > @
        "mismatches: [%s]"
        (text ", ").joined mismatches

    [] > description-of
      joined. > @
        text " and "
        mapped.
          list
            matchers
          [i]
            i.description-of > @

  # Matcher for array whose elements satisfy a sequence of matchers.
  # The array size must equal the number of element matchers.
  [matchers...] > array-each

    memory 0 > mismatches

    [] > check-length
      eq. > @
        matchers.length
        act.length

    [a] > match
      if. > @
        check-length
        seq
          mismatches.write *
          reducedi.
            list
              a
            TRUE
            [acc i el]
              and. > @
                acc
                if.
                  (matchers.at i).match el
                  TRUE
                  seq
                    mismatches.write
                      mismatches.with
                        describe-mismatch.
                          matchers.at i
                          act
                    FALSE
        FALSE

    [act] > describe-mismatch
      sprintf > @
        "mismatches: [%s]"
        (text ", ").joined mismatches

    [] > description-of
      (text " and ").joined > @
        mapped.
          list
            matchers
          [i]
            i.description-of > @

  [str] > contains-string

    [a] > match
      contains. > @
        text a
        str

    [act] > describe-mismatch
      sprintf > @
        "was <%s>"
        act

    [] > description-of
      sprintf > @
        "string contains: <%s>"
        str
